apply plugin: 'com.android.application'
apply plugin: 'org.ajoberstar.grgit'

ext {
  gitVersionName = grgit.describe()
  dirty = !grgit.status().clean
  if(dirty) gitVersionName = gitVersionName + '-dirty'
  gitVersionCode = grgit.log().size()
}

task printVersionName() {
    println("$gitVersionName")
}

android {
    defaultConfig {
        versionName "${gitVersionName}"
    }
}

android {
    compileSdkVersion 33
    namespace "info.zonglovani.mobile.app"
    defaultConfig {
        applicationId "info.zonglovani.mobile.app"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode gitVersionCode
        versionName "${gitVersionName}"
        setProperty("archivesBaseName", "Zongleruv-slabikar")
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

task gethtml(type: Exec, dependsOn: ['generateIcons', 'stahni']) {
    commandLine 'bash', 'uprav.sh'
    workingDir '..'
}

task stahni(type: Exec) {
    commandLine 'bash', './stahni.sh'
    workingDir '..'
}

task generateIcons(type: Exec) {
    commandLine 'bash', './icons.sh'
    workingDir '..'
}

task customClean(type: Delete) {
    delete "${projectDir}/../app/src/main/assets/www"
    delete "src/main/res/mipmap-hdpi"
    delete "src/main/res/mipmap-mdpi"
    delete "src/main/res/mipmap-xhdpi"
    delete "src/main/res/mipmap-xxhdpi"
    delete "src/main/res/mipmap-xxxhdpi"
}
clean.dependsOn customClean

preBuild.dependsOn(gethtml)

def props = new Properties()

props.load(new FileInputStream("${System.env.HOME}/android-sec/zonglovani.properties"))

android.signingConfigs.release.storeFile rootProject.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword
